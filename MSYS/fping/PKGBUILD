# Based on Arch Linux PKGBUILD with minor modifications

pkgname=fping
pkgver=5.0
pkgrel=1
pkgdesc='Utility to ping multiple hosts at once'
url='https://www.fping.org/'
arch=('x86_64')
license=('custom')
makedepends=('gcc')
install=${pkgname}.install
source=(https://www.fping.org/dist/${pkgname}-${pkgver}.tar.gz{,.asc}
icmp.h
01-icmp_h.patch)
sha512sums=('c843f7bb7c4e3289c4dd9eb8ae2ab93a363316c0000aac9187f79580a3d2505df179f2e0ae9e0a791c74bfc9e17c476874f8e122dfa6ad74930498c4e02d0de3'
            'SKIP'
            'e117ea6831d98b3b3a80dd823a7cef573bbed934ec0bf9bcee220642e9fcc98dbefa8e8fe18eafce1ee6a23fbfa7e5d6db845ab45c43173a95ac4da473b448dd'
            'ff2a169e357af80b7544a82bf3859a288047a000f3e57e9dc8aa973d279d0d0a0d345ee31a3238eb48e5412495b143f6d3de42fa084542c9f1765da624fbf857')
b2sums=('6dbb6708ecb55c1f1281bcd9e298475a1ad3a21aa81d8488b61cb39ad07d1d7285247a3c39883cbaae0b1a0e4403b6340ea7ec43411b81546f0e8eeefb02ef88'
        'SKIP'
        'SKIP'
        'SKIP')
validpgpkeys=('8D1E89B8FBA920A5A609F2615A11A22BC5A1F734') # David Schweikert <david@schweikert.ch>


prepare() {
  cd ${srcdir}
  patch --forward --strip=0 --input="${srcdir}/01-icmp_h.patch"
  # See here: https://niversal.wordpress.com/2017/02/22/fping-on-windows/
  cat "${srcdir}/icmp.h" > /usr/include/cygwin/icmp.h
  cat "${srcdir}/icmp.h" > /usr/include/icmp.h
}

build() {
  
  cd ${pkgname}-${pkgver}
  autoreconf -i
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --mandir=/usr/share/man \
    --enable-ipv4 \
    --disable-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
